alias i S0;
alias Cause S1;
alias VPagenum S2;
alias Ip_addr S3;

Cause = EFR % 10; //get the values from the EFR
VPagenum = (EFR / 10) % 100;
Ip_addr = (EFR/1000);

if(Cause != 0) then //not a page fault, so exit after displaying the apprpriate message
	print (EFR % 10);
	alias physicalSP S8;
	physicalSP = [PTBR + 2*(SP/512)]*512 + (SP%512);
	[physicalSP - 1] = 10;
	inline "JMP 11776"; 
endif;

if([PTBR + 2*VPagenum] == -1) then //illegal memory access
	inline "JMP 11776";
endif;

alias counter S4;
counter = 0;
while(counter < 64) do //find a free page in memory
	if([1280 + counter]==0) then
		[1280 + counter] = 1;
		i = counter;
		break;
	endif;
	counter = counter + 1;
endwhile;

alias j S5;
alias physicalpage S6;

j = counter;

if(counter == 64) then //if no free page, replace an existing page
	counter = ((PTBR - 1024)/8 + 1) % 32; //search in page table of all processes
	while(1) do
		i = 0;
		while(i<3) do 
			if([1024 + 8*counter + 2*i + 1]=="01" && [1280 + [1024 + 8*counter + 2*i]]<=1) then //if valid page not shared by any other process
				j = 448;
				while(j<512) do //find a free disk block to swap to
					if([6*512 + j]==0) then
						break;
					endif;
					j = j + 1;
				endwhile;
				
				if(j==512) then //exit if no free block
					inline "JMP 23*512";
				endif;
	
				physicalpage = [1024 + 8*counter + 2*i]; //get the physical page(disk block here) corresponding to this entry
				store([1024 + 8*counter + 2*i],j); //store it to the disk(swapping it out)
				[6*512 + j] = 1; //set the block as used in the df
				[1024 + 8*counter + 2*i] = j; //the page table entry now stores this disk block number
				[1024 + 8*counter + 2*i + 1] = "00"; //invalidate the entry
				break;
			endif;

			if([1024 + 8*counter + 2*i + 1]=="11") then //set the reference bit to 0
				[1024 + 8*counter + 2*i + 1] = "01";
			endif;
			i = i + 1;
		endwhile;
		if(i < 3) then //found a page to replace. Can stop searching now.
			break;
		endif;
		counter = (counter + 1) % 32;
	endwhile;
	j = physicalpage;
endif;



alias physvpage S7;
physvpage = [PTBR + 2*VPagenum]; //get the disk block pointed to by the page table entry
if(physvpage>=21 || physvpage <=447) then //if not a swap block, simply load it to the free memory page
	load(j,physvpage);
endif;

if(physvpage>=448) then //if a swap block, decrement its entry in the df by 1 also
	load(j,physvpage);
	[6*512 + physvpage] = [6*512 + physvpage] - 1;
endif;

[PTBR + 2*VPagenum] = j; //set the page table entry to the memory page found
[PTBR + 2*VPagenum + 1] = "11"; //set the entry to referenced and valid

SP = SP + 1;
alias physicalSP S8;
physicalSP = [PTBR + 2*(SP/512)]*512 + (SP%512);
[physicalSP] = Ip_addr; //store the IP value on the stack top
ireturn;

				
















				
		
